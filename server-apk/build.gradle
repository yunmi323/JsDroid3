apply plugin: 'com.android.application'

android {
    compileSdkVersion 29


    defaultConfig {
        applicationId "com.jsdroid.server_apk"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode JSDROID_VERSION_CODE
        versionName JSDROID_VERSION

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //配置编译方式
        externalNativeBuild {
            ndkBuild {
                //一般真机cpu架构是arm
                abiFilters 'armeabi-v7a'
                //模拟器是x86
                abiFilters 'x86'
            }
        }
        //配置 打包（决定包在什么平台可以运行）
        ndk {
            //一般真机cpu架构是arm
            abiFilters 'armeabi-v7a'
            //模拟器是x86
            abiFilters 'x86'
        }
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            //so库路径
            jniLibs.srcDirs = ['libs']
        }
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    compileOnly project(":ipc")
    compileOnly project(":api")
    compileOnly files("sdk/sdk19-1.0.jar")
    compileOnly(files("../libs/android.jar"))
    compileOnly project(":app-hidden-api")
    compileOnly "org.apache.commons:commons-io:1.3.2"
    compileOnly "com.alibaba:fastjson:$FAST_JSON_VERSION"

    implementation project(":groovy")
    implementation(files("../libs/groovy.jar"))

    implementation 'com.google.android.tools:dx:1.7'
    implementation "org.jsoup:jsoup:1.11.3"
    //server
    implementation 'com.koushikdutta.async:androidasync:2.+'

    implementation("eu.chainfire:libsuperuser:1.0.0.201811281328")
//    implementation "commons-codec:commons-codec:1.14"

    implementation 'androidx.core:core:1.2.0'
    implementation 'androidx.sqlite:sqlite:2.1.0'

}
build.doLast {
    //将build之后的apk复制到runner/src/main/res/raw/server.apk
    project.buildOutputs.forEach({
        v ->
            if (v.name == "release") {
                copy {
                    from(v.outputFile)
                    into(new File(project.rootDir, "runner/src/main/res/raw"))
                    rename {
                        oldName ->
                            "server.apk"
                    }
                }
            }

    })
}


//先build app，然后执行此任务
task jsdroidApi(type: Zip, dependsOn: ['build'], group: "jsdroid") {
    metadataCharset "UTF-8"
    extension "zip"
    archiveName = "jsdroid-api.jar"
    destinationDir file('build/')
    from fileTree("build/intermediates/javac/release/compileReleaseJavaWithJavac/classes")

}